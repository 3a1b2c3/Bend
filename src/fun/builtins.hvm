data String = (cons head ~tail) | (nil)
data List   = (cons head ~tail) | (nil)
data Result = (ok val) | (err val)
data Nat    = (succ ~pred) | (zero)

// MAP Impl

data Map    = (node value ~left ~right) | (leaf)

Map/empty = Map/leaf

Map/get map key =
  match map {
    Map/leaf: (*, map)
    Map/node:
      switch _ = (== 0 key) {
        0: switch _ = (% key 2) {
          0:
            let (got, rest) = (Map/get map.left (/ key 2))
            (got, (Map/node map.value rest map.right))
          _:
            let (got, rest) = (Map/get map.right (/ key 2))
            (got, (Map/node map.value map.left rest))
        }
        _: (map.value, map)
      }
  }

Map/set map key value =
  match map {
    Map/node:
      switch _ = (== 0 key) {
        0: switch _ = (% key 2) {
          0: (Map/node map.value (Map/set map.left (/ key 2) value) map.right)
          _: (Map/node map.value map.left (Map/set map.right (/ key 2) value))
        }
        _: (Map/node value map.left map.right)
      }
    Map/leaf:
      switch _ = (== 0 key) {
        0: switch _ = (% key 2) {
          0: (Map/node * (Map/set Map/leaf (/ key 2) value) Map/leaf)
          _: (Map/node * Map/leaf (Map/set Map/leaf (/ key 2) value))
        }
        _: (Map/node value Map/leaf Map/leaf)
      }
  }

// IO Impl

STRING_NIL_TAG  = 0
STRING_CONS_TAG = 1

IO_DONE_TAG       = 0
IO_PUT_TEXT_TAG   = 1
IO_GET_TEXT_TAG   = 2
IO_WRITE_FILE_TAG = 3
IO_READ_FILE_TAG  = 4
IO_GET_TIME_TAG   = 5
IO_SLEEP_TAG      = 6
IO_DRAW_IMAGE_TAG = 7

data IO
  = (Done term)
  | (PutText   text      cont)
  | (GetText             cont)
  | (WriteFile file data cont)
  | (ReadFile  file      cont)
  | (GetTime             cont)
  | (Sleep     time      cont)
  | (DrawImage tree      cont)

MkStr (String/cons x xs) = λt (t STRING_CONS_TAG x (MkStr xs))
MkStr (String/nil)       = λt (t STRING_NIL_TAG)

MkIO (IO/Done term)                = λt (t IO_DONE_TAG term)
MkIO (IO/PutText text cont)        = λt (t IO_PUT_TEXT_TAG text λx (MkIO (cont x)))
MkIO (IO/GetText cont)             = λt (t IO_GET_TEXT_TAG λx (MkIO (cont x)))
MkIO (IO/WriteFile file data cont) = λt (t IO_WRITE_FILE_TAG file data λx (MkIO (cont x)))
MkIO (IO/ReadFile file cont)       = λt (t IO_READ_FILE_TAG file λx(MkIO (cont x)))
MkIO (IO/GetTime cont)             = λt (t IO_GET_TIME_TAG λx (MkIO (cont x)))
MkIO (IO/Sleep time cont)          = λt (t IO_SLEEP_TAG time λx (MkIO (cont x)))
MkIO (IO/DrawImage tree cont)      = λt (t IO_DRAW_IMAGE_TAG tree λx (MkIO (cont x)))
